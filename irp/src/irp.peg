irp <- general_spec bitspec_irstream definitions* parameter_specs? EOI;

general_spec <- ws "{" (ws general_item ("," ws general_item)*)? "}" ws;

general_item <- "msb" ws
    / "lsb" ws
    / number_decimals ws ("u" / "p" / "k" / "%") ws
    / number_decimals ws;

number_decimals <- re#\d+\.\d+#
    / re#\d+#;

definitions <- '{' ws definition ("," ws definition)* '}' ws
    / '{' ws '}' ws;

definition <- XID_IDENTIFIER ws '=' ws expression ws;

bitspec_definition <- XID_IDENTIFIER ws '=' ws primary_item ws;

bitspec <- "<" ws bare_bitspec ("|" ws bare_bitspec)* ">" ws;

bare_bitspec <- (bitspec_item ("," ws bitspec_item)*)? ;

bare_irstream <- (irstream_item ("," ws irstream_item)*)? ;

irstream <- '(' ws bare_irstream ')' ws repeat_marker? ws;

repeat_marker <- '*' / '+' / number '+'?;

irstream_item <- variation
    / bit_field
    / definition
    / duration
    / irstream
    / bitspec_irstream;

bitspec_item <- variation
    / bit_field
    / bitspec_definition
    / duration
    / irstream
    / bitspec_irstream;

bitspec_irstream <- bitspec irstream;

duration <- ('-' / '^')? name_or_number ('m' / 'u' / 'p')? ws;

name_or_number <- XID_IDENTIFIER / number_decimals;

variation <- alternative alternative alternative?;

alternative <- '[' ws bare_irstream ']' ws;

parameter_specs <- "[" ws parameter_spec ("," ws parameter_spec)* "]" ws
    / "[" ws "]" ws;

parameter_spec <- XID_IDENTIFIER ws "@"? ws ":" ws number ws ".." ws number ws ('=' expression)? ws;

number <- re#0x[0-9a-fA-F]+#
    / re#0b[01]+#
    / re#\d+#
    / "UINT8_MAX"
    / "UINT16_MAX"
    / "UINT32_MAX"
    / "UINT64_MAX";

primary_item <- number ws
    / XID_IDENTIFIER ws
    / "(" ws expression ")" ws;

bit_field <- "~"? ws primary_item ":" ws "-"? ws primary_item (":" ws primary_item)? ws
    / "~"? ws primary_item "::" ws primary_item ws;

expression <- expression '?' ws expression2 ':' ws expression2
    / expression2;

expression2 <- expression2 '||' ws expression3
    / expression3;

expression3 <- expression3 '&&' ws expression4
    / expression4;

expression4 <- expression4 '|' ws expression5
    / expression5;

expression5 <- expression5 '^' ws expression6
    / expression6;

expression6 <- expression6 '&' ws expression7
    / expression7;

expression7 <- expression7 ('!=' / '==') ws expression8
    / expression8;

expression8 <- expression8 ('<=' / '>=' / '<' / '>') ws expression9
    / expression9;

expression9 <- expression9 ('<<' / '>>') ws expression10
    / expression10;

expression10 <- expression10 ('+' / '-') ws expression11
    / expression11;

expression11 <- expression11 ('*' / '/' / '%') ws expression12
    / expression12;

expression12 <- expression13 "**" ws expression12
    / expression13;

expression13 <- "#" ws expression14
    / expression14;

expression14 <- "!" ws expression15
    / expression15;

expression15 <- "-" ws expression16
    / expression16;

expression16 <- "~" ws expression17
    / expression17;

expression17 <- bit_field
    / primary_item;

ws <- WHITESPACE re#/\\*.*?\\*/# ws
    / WHITESPACE re#//[^\n]*# ws
    / WHITESPACE;
